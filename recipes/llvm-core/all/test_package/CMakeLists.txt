cmake_minimum_required(VERSION 3.15)
project(test_package LANGUAGES CXX)

find_package(LLVM REQUIRED CONFIG)

add_executable(${PROJECT_NAME} test_package.cpp)

if (TARGET LLVM)
    target_link_libraries(${PROJECT_NAME} PRIVATE LLVM)
else()
    llvm_map_components_to_libnames(llvm_libs interpreter nativecodegen irreader)
    target_link_libraries(${PROJECT_NAME} PRIVATE ${llvm_libs})
endif()

macro(test_llvm_cmake_variable VARIABLE)
    if(NOT DEFINED ${VARIABLE})
        message(FATAL_ERROR "${VARIABLE} is not defined")
    endif()
    message(STATUS "${VARIABLE}: ${${VARIABLE}}")
endmacro()

message(STATUS "Testing LLVM Build Module Variables")
test_llvm_cmake_variable(LLVM_PACKAGE_VERSION)
test_llvm_cmake_variable(LLVM_BUILD_TYPE)
test_llvm_cmake_variable(LLVM_CMAKE_DIR)
test_llvm_cmake_variable(LLVM_TOOLS_BINARY_DIR)
test_llvm_cmake_variable(LLVM_ALL_TARGETS)
test_llvm_cmake_variable(LLVM_TARGETS_TO_BUILD)
test_llvm_cmake_variable(LLVM_TARGETS_WITH_JIT)
test_llvm_cmake_variable(LLVM_NATIVE_ARCH)

if(NOT TARGET llvm-link)
   message(FATAL_ERROR "llvm-link target is missing - executables targets have not been exported correctly")
endif()
get_target_property(llvm_link_location llvm-link IMPORTED_LOCATION)
if(NOT EXISTS ${llvm_link_location})
   message(FATAL_ERROR "Invalid llvm-link target location: ${llvm_link_location}")
endif()
